const std = @import("std");
const Bus = @import("memory.zig").Bus;

pub const Channel = struct {
    base_addr: u32 = 0, // MADR (Memory Address)
    block_control: u32 = 0, // BCR  (Block Control)
    control: u32 = 0, // CHCR (Channel Control)

    transfer_active: bool = false,
    words_remaining: u32 = 0,
    linked_list_next: u32 = 0,
    
    chop_dma_window: u32 = 0,
    chop_cpu_window: u32 = 0,
    chop_is_cpu_turn: bool = false,
    chop_counter: u32 = 0,

    pub fn read(self: *const Channel, offset: u32) u32 {
        return switch (offset) {
            0x0 => self.base_addr,
            0x4 => self.block_control,
            0x8 => self.control,
            else => 0,
        };
    }

    pub fn write(self: *Channel, offset: u32, value: u32) void {
        switch (offset) {
            0x0 => self.base_addr = value & 0x00FFFFFF, // 24-bit address
            0x4 => self.block_control = value,
            0x8 => {
                const was_busy = (self.control & (1 << 24)) != 0;
                const becomes_busy = (value & (1 << 24)) != 0;
                self.control = value;

                if (!was_busy and becomes_busy) {
                    self.startTransfer();
                } else if (was_busy and !becomes_busy) {
                    self.transfer_active = false;
                }
            },
            else => {},
        }
    }

    fn startTransfer(self: *Channel) void {
        const sync_mode = (self.control >> 9) & 3;
        
        if (sync_mode == 0) {
            self.words_remaining = self.block_control & 0xFFFF;
            if (self.words_remaining == 0) self.words_remaining = 0x10000;
        } else if (sync_mode == 1) {
            const words: u32 = if ((self.block_control & 0xFFFF) == 0) 0x10000 else self.block_control & 0xFFFF;
            const blocks: u32 = if (((self.block_control >> 16) & 0xFFFF) == 0) 0x10000 else (self.block_control >> 16) & 0xFFFF;
            self.words_remaining = words * blocks;
        } else if (sync_mode == 2) {
            self.words_remaining = 0xFFFFFFFF; // special marker
        }

        const chop_enable = (self.control & (1 << 8)) != 0;
        if (chop_enable and sync_mode == 0) {
            const dma_win = (self.control >> 16) & 7;
            const cpu_win = (self.control >> 20) & 7;
            self.chop_dma_window = @as(u32, 1) << @as(u5, @truncate(dma_win));
            self.chop_cpu_window = @as(u32, 1) << @as(u5, @truncate(cpu_win));
            self.chop_is_cpu_turn = false;
            self.chop_counter = self.chop_dma_window;
        } else {
            self.chop_dma_window = 0;
            self.chop_cpu_window = 0;
            self.chop_is_cpu_turn = false;
            self.chop_counter = 0;
        }

        self.transfer_active = true;
    }
};

pub const Dma = struct {
    const Self = @This();

    channels: [7]Channel = [_]Channel{.{}} ** 7,

    dpcr: u32 = 0x07654321,
    dicr: u32 = 0,

    pub fn init() Self {
        return .{};
    }

    pub fn read(self: *const Self, offset: u32) u32 {
        const channel_idx = (offset >> 4) & 0x7;

        if (offset < 0x70) {
            return self.channels[channel_idx].read(offset & 0xF);
        }

        return switch (offset) {
            0x70 => self.dpcr,
            0x74 => self.dicr,
            else => {
                std.log.warn("Unhandled DMA read at offset 0x{X:0>2}", .{offset});
                return 0;
            },
        };
    }

    pub fn write(self: *Self, bus: *Bus, offset: u32, value: u32) void {
        const channel_idx = (offset >> 4) & 0x7;

        if (offset < 0x70) {
            self.channels[channel_idx].write(offset & 0xF, value);
            return;
        }

        switch (offset) {
            0x70 => self.dpcr = value,
            0x74 => {
                const rw_mask = 0x00FF803F;
                const old_val = self.dicr;
                const clear_mask = (value >> 24) & 0x7F;
                const preserve_flags = (value & (1 << 23)) != 0;
                const new_flags = if (preserve_flags) ((old_val >> 24) & 0x7F) & ~clear_mask else 0;

                self.dicr = (value & rw_mask) | (@as(u32, new_flags) << 24) | (old_val & (1 << 31));
                self.updateDicr31(bus);
            },
            else => std.log.warn("Unhandled DMA write at offset 0x{X:0>2}", .{offset}),
        }
    }

    pub fn updateDicr31(self: *Self, bus: *Bus) void {
        const force_irq = (self.dicr >> 15) & 1;
        const irq_en = (self.dicr >> 16) & 0x7F;
        const master_en = (self.dicr >> 23) & 1;
        const irq_flags = (self.dicr >> 24) & 0x7F;

        const master_irq = force_irq == 1 or (master_en == 1 and (irq_en & irq_flags) != 0);
        const old_master = (self.dicr & (1 << 31)) != 0;

        if (master_irq) {
            self.dicr |= (1 << 31);
            if (!old_master) {
                bus.i_stat |= (1 << 3);
            }
        } else {
            self.dicr &= ~@as(u32, 1 << 31);
        }
    }

    pub fn step(self: *Self, bus: *Bus) void {
        for (0..7) |i| {
            const channel = &self.channels[i];
            if (!channel.transfer_active) continue;

            const dpcr_channel_en = (self.dpcr >> @as(u5, @truncate(i * 4 + 3))) & 1;
            if (dpcr_channel_en == 0) continue;

            if (channel.chop_dma_window > 0) {
                if (channel.chop_is_cpu_turn) {
                    if (channel.chop_counter > 0) channel.chop_counter -= 1;
                    if (channel.chop_counter == 0) {
                        channel.chop_is_cpu_turn = false;
                        channel.chop_counter = channel.chop_dma_window;
                    }
                    continue;
                }
            }

            const sync_mode = (channel.control >> 9) & 3;

            if (sync_mode == 1) {
                if (i == 3 and bus.cdrom.data_fifo_empty) continue;
            }

            var done = false;
            if (sync_mode == 2) {
                done = self.doLinkedListWord(bus, i);
            } else {
                done = self.doBlockCopyWord(bus, i);
            }

            if (channel.chop_dma_window > 0 and !channel.chop_is_cpu_turn) {
                if (channel.chop_counter > 0) channel.chop_counter -= 1;
                if (channel.chop_counter == 0) {
                    channel.chop_is_cpu_turn = true;
                    channel.chop_counter = channel.chop_cpu_window;
                }
            }

            if (done) {
                channel.transfer_active = false;
                channel.control &= ~@as(u32, 1 << 24);
                if (sync_mode == 0) channel.control &= ~@as(u32, 1 << 28);
                
                self.dicr |= (@as(u32, 1) << @as(u5, @truncate(24 + i)));
                self.updateDicr31(bus);
            }
        }
    }

    fn doBlockCopyWord(self: *Self, bus: *Bus, channel_idx: usize) bool {
        const channel = &self.channels[channel_idx];
        const addr = channel.base_addr & 0x1FFFFC;

        const direction = (channel.control >> 0) & 1;
        const step_val: u32 = if ((channel.control >> 1) & 1 == 0) 4 else 0xFFFFFFFC;

        if (direction == 0) {
            if (channel_idx == 1) bus.write32(addr, bus.mdec.readData())
            else if (channel_idx == 2) bus.write32(addr, bus.gpu.readData())
            else if (channel_idx == 3) bus.write32(addr, bus.cdrom.readDataWord())
            else if (channel_idx == 4) {
                const low = bus.spu.dmaReadSram();
                const high = bus.spu.dmaReadSram();
                bus.write32(addr, (@as(u32, high) << 16) | low);
            } else if (channel_idx == 6) {
                const next = if (channel.words_remaining == 1) 0x00FFFFFF else (addr -% 4) & 0xFFFFFF;
                bus.write32(addr, next);
                if (channel.words_remaining == 1) {
                    channel.base_addr = addr;
                } else {
                    channel.base_addr = next & 0x1FFFFC;
                }
            } else bus.write32(addr, 0);
        } else {
            const val = bus.read32(addr);
            if (channel_idx == 0) bus.mdec.writeData(val)
            else if (channel_idx == 2) bus.gpu.writeGp0(val)
            else if (channel_idx == 4) {
                bus.spu.writeSram(@truncate(val & 0xFFFF));
                bus.spu.writeSram(@truncate(val >> 16));
            }
        }

        if (channel_idx != 6) {
            channel.base_addr = (addr +% step_val) & 0x1FFFFC;
        }

        if (channel.words_remaining > 0) {
            channel.words_remaining -= 1;
        }
        
        return channel.words_remaining == 0;
    }

    fn doLinkedListWord(self: *Self, bus: *Bus, channel_idx: usize) bool {
        const channel = &self.channels[channel_idx];
        const addr = channel.base_addr & 0x1FFFFC;
        // std.log.warn("LL Word: addr={X}, words={X}", .{addr, channel.words_remaining});


        if (channel.words_remaining == 0xFFFFFFFF) {
            // Read header
            const header = bus.read32(addr);
            const words = (header >> 24) & 0xFF;
            
            if (words > 0) {
                channel.words_remaining = words;
                channel.linked_list_next = header & 0x1FFFFC;
                channel.base_addr = (addr +% 4) & 0x1FFFFC;
            } else {
                if ((header & 0x00FFFFFF) == 0x00FFFFFF) return true;
                channel.base_addr = header & 0x1FFFFC;
            }
        } else {
            // Read payload
            const command = bus.read32(addr);
            if (channel_idx == 2) {
                bus.gpu.writeGp0(command);
            }
            
            channel.base_addr = (addr +% 4) & 0x1FFFFC;
            channel.words_remaining -= 1;
            
            if (channel.words_remaining == 0) {
                // Packet complete, jump to next header
                if (channel.linked_list_next == 0x1FFFFC) return true; // Actually 0xFFFFFF end marker
                
                channel.base_addr = channel.linked_list_next;
                channel.words_remaining = 0xFFFFFFFF; // Reset to header mode
            }
        }

        return false;
    }
};
